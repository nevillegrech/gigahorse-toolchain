.type PrivateContext = [block: Block, rest: PrivateContext]

.type Context = [ publicFun:symbol, pri: PrivateContext ]

// find number of jumps to estimate best context depth
.decl MaxContextDepth(sigHash: symbol, d: number)

.decl InputMaxContextDepth(d: number)
.input InputMaxContextDepth(filename="MaxContextDepth.csv")

MaxContextDepth(sigHash, d) :-
  (postTrans.PublicFunction(_, sigHash);
   sigHash = FALLBACK_FUNCTION_SIGHASH),
   InputMaxContextDepth(d).

#ifndef MAX_CONTEXT_DEPTH
MaxContextDepth(sigHash, 8) :-
  (postTrans.PublicFunction(_, sigHash);
   sigHash = FALLBACK_FUNCTION_SIGHASH),
  !InputMaxContextDepth(_).
#else
MaxContextDepth(sigHash, MAX_CONTEXT_DEPTH) :-
  (postTrans.PublicFunction(_, sigHash);
   sigHash = FALLBACK_FUNCTION_SIGHASH),
  !InputMaxContextDepth(_).
#endif

.decl InitialPrivateContext(ctx : PrivateContext)
InitialPrivateContext(nil).

.decl DecomposePrivateContext(priCtx: PrivateContext, block: Block, rest: PrivateContext)

.decl DropLast(ctx : PrivateContext, newCtx : PrivateContext)
DropLast(ctx, nil) :-
  DecomposePrivateContext(ctx, _, nil).

DecomposePrivateContext(newCtx, block, newRest),
PrivateContextDepth(newCtx, depth - 1),
DropLast(ctx, newCtx) :-
  DecomposePrivateContext(ctx, block, rest),
  PrivateContextDepth(ctx, depth),
  DropLast(rest, newRest),
  newCtx = [ block, newRest ].
  .plan 1:(2,1,3), 2:(3,1,2)

.decl PrivateContextDepth(ctx : PrivateContext, depth : number)
PrivateContextDepth(init, 0) :-
  InitialPrivateContext(init).

PrivateContextDepth(ctx, depth + 1) :-
  DecomposePrivateContext(ctx, _, rest),
  PrivateContextDepth(rest, depth).
  .plan 1:(2,1)

.decl TruncateContext(publicFun:symbol, ctx: PrivateContext, newCtx : PrivateContext)

TruncateContext(pub, ctx, newCtx) :-
  PrivateContextDepth(ctx, maxDepth),
  MaxContextDepth(pub, maxDepth),
  DropLast(ctx, newCtx).
  .plan 1:(3,1,2)

TruncateContext(pub, ctx, ctx) :-
  PrivateContextDepth(ctx, depth),
  MaxContextDepth(pub, maxDepth),
  depth < maxDepth.

.decl InitialContext(ctx : Context)

InitialContext([FALLBACK_FUNCTION_SIGHASH, initialPrivateContext]) :-
  InitialPrivateContext(initialPrivateContext).


// inverse-escape analysis
.decl Block_Uses_Local(block: Block, var: VariableOrStackIndex)

Block_Uses_Local(block, var) :-
  postTrans.Statement_Block(stmt, block),
  postTrans.Statement_Uses_Local(stmt, var, _).

// We're only interested in private functions
// No ambiguity for public functions
.decl PrivateFunctionCallOrReturn(caller: Block)

PrivateFunctionCallOrReturn(caller) :-
  StaticBlockJumpTarget(caller, _),
  postTrans.Statement_Block(stmt, caller),
  postTrans.Statement_Defines(stmt, var),
  postTrans.Variable_Value(var, val),
  postTrans.JUMPDEST(@cast_to_symbol(val)),
  !Block_Uses_Local(caller, var).

PrivateFunctionCallOrReturn(caller) :-
  postTrans.Statement_Block(stmt, caller),
  postTrans.JUMP(stmt),
  !postTrans.ImmediateBlockJumpTarget(caller, _).


Context_PublicFunction(ctx, pubFun):-
  ReachableContext(ctx, _),
  ctx = [pubFun, callCtx],
  callCtx = callCtx.
  
.decl StaticBlockJumpTarget(caller: Block, target: Value)

StaticBlockJumpTarget(caller, target) :-
  postTrans.ImmediateBlockJumpTarget(caller, targetVar),
  postTrans.Variable_Value(targetVar, target),
  postTrans.JUMPDEST(@cast_to_symbol(target)).

.decl MergeContext(ctx: Context, caller: Block, newContext: Context)

.decl DecomposeContext(ctx: Context, publicFun: symbol, private: PrivateContext)

DecomposeContext(ctx, publicFun, private):-
  ReachableContext(ctx, _), ctx = [publicFun, private].

// Trivial control flow case for when there is no function call
// OR max conext depth is zero
// Split into two rules to add plan.
MergeContext(ctx, caller, ctx) :-
  ReachableContext(ctx, caller),
#ifndef NO_PUBLIC_CONTEXT
  !postTrans.PublicFunction(caller, _),
#endif
  !PrivateFunctionCallOrReturn(caller).

MergeContext(ctx, caller, ctx) :-
  ReachableContext(ctx, caller),
#ifndef NO_PUBLIC_CONTEXT
  !postTrans.PublicFunction(caller, _),
#endif
  DecomposeContext(ctx, pub, _),
  MaxContextDepth(pub, 0).
  .plan 1:(2,3,1)

// Complex control flow case

DecomposePrivateContext(newPrivateContext, caller, cutDownPri),
MergeContext(ctx, caller, [pub, newPrivateContext]) :-
  ReachableContext(ctx, caller),
  PrivateFunctionCallOrReturn(caller),
  DecomposeContext(ctx, pub, pri),
  TruncateContext(pub, pri, cutDownPri),
#ifndef NO_PUBLIC_CONTEXT
  !postTrans.PublicFunction(caller, _),
#endif
  newPrivateContext = [caller, cutDownPri].
.plan 1:(3,1,2,4), 2:(4,3,1,2)

#ifndef NO_PUBLIC_CONTEXT
MergeContext(ctx, caller, newContext) :-
  ReachableContext(ctx, caller),
  postTrans.PublicFunction(caller, sigHash),
  !MaxContextDepth(sigHash, -1),
  DecomposeContext(ctx, _, pri),
  newContext = [sigHash, pri].
 .plan 1:(3,1,2)
#endif

