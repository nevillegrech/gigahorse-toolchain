
/**
  Storage modeling client helpers
  Relations that can be computed using relations in the API but are useful standalone
*/

// Doesn't contain TightlyPackedVariable, maybe revisit
.decl StorageConstruct_ParentAndOffset(cons: StorageConstruct, parentCons: StorageConstruct, offset: number)


.decl StorageConstructPredecesor(cons: StorageConstruct, predCons: StorageConstruct)

StorageConstructPredecesor(cons, parentCons):-
  StorageConstruct_ParentAndOffset(cons, parentCons, _).

StorageConstructPredecesor(cons, predParentCons):-
  StorageConstructPredecesor(cons, predCons),
  StorageConstruct_ParentAndOffset(predCons, predParentCons, _).

/**
  Top level identifier for every construct.
  Even for complex data structures (nested, including structs etc) it will unify all inferences in a
  top level one containing its kind ("stor" for simple vars, "mapping", "array") and its declared offset.
*/
.decl Construct_TopLevelName(loc: StorageLocation, cons: StorageConstruct, name: symbol)
DEBUG_OUTPUT(Construct_TopLevelName)

Construct_TopLevelName($Storage(), $Variable($Constant(const)), SIMPLE_STORAGE_VAR(const)):-
  IsStorageConstruct($Storage(), $Variable($Constant(const))).

Construct_TopLevelName($Storage(), $TightlyPackedVariable($Constant(const), low, high), MERGED_STORAGE_VAR(const, low, high)):-
  IsStorageConstruct($Storage(), $TightlyPackedVariable($Constant(const), low, high)).

Construct_TopLevelName($TransientStorage(), $Variable($Constant(const)), SIMPLE_TSTORAGE_VAR(const)):-
  IsStorageConstruct($TransientStorage(), $Variable($Constant(const))).

Construct_TopLevelName($TransientStorage(), $TightlyPackedVariable($Constant(const), low, high), MERGED_TSTORAGE_VAR(const, low, high)):-
  IsStorageConstruct($TransientStorage(), $TightlyPackedVariable($Constant(const), low, high)).

Construct_TopLevelName($Storage(), $Mapping($Constant(const)), cat("mapping", const)):-
  IsStorageConstruct($Storage(), $Mapping($Constant(const))).

Construct_TopLevelName($Storage(), $Array($Constant(const)), cat("array", const)):-
  IsStorageConstruct($Storage(), $Array($Constant(const))).

Construct_TopLevelName($Storage(), $StaticArray($Constant(const), arraySize), cat("array", const)):-
  IsStorageConstruct($Storage(), $StaticArray($Constant(const), arraySize)).


Construct_TopLevelName(loc, cons, name):-
  Construct_TopLevelName(loc, predParentCons, name),
  StorageConstructPredecesor(cons, predParentCons).